CMAKE_MINIMUM_REQUIRED (VERSION 2.8.12)

PROJECT (Oyjl)
SET (PROJECT_UP_NAME "OYJL" )
SET (PROJECT_DOWN_NAME "oyjl" )

SET( ${PROJECT_UP_NAME}_VERSION_MAJOR 1)
SET( ${PROJECT_UP_NAME}_VERSION_MINOR 0)
SET( ${PROJECT_UP_NAME}_VERSION_MICRO 0)
SET( ${PROJECT_UP_NAME}_VERSION "${${PROJECT_UP_NAME}_VERSION_MAJOR}.${${PROJECT_UP_NAME}_VERSION_MINOR}.${${PROJECT_UP_NAME}_VERSION_MICRO}" )

SET (PACKAGE_NAME "${PROJECT_DOWN_NAME}" )
SET (PACKAGE_DESCRIPTION "Oyjl API provides a platformindependent C interface for JSON I/O, conversion to and from XML + YAML, string helpers, file reading, testing and argument handling.")

SET( ${PROJECT_NAME}_VERSION_MAJORMINOR "${${PROJECT_NAME}_VERSION_MAJOR}.${${PROJECT_NAME}_VERSION_MINOR}" )

INCLUDE(GNUInstallDirs)

OPTION(ENABLE_TESTS_${PROJECT_UP_NAME} "Build tests (requires static libs)" ON)
OPTION(ENABLE_TOOLS_${PROJECT_UP_NAME} "Build tools" ON)
OPTION(ENABLE_SHARED_LIBS_${PROJECT_UP_NAME} "Build dynamic link libs" ON)
OPTION(ENABLE_STATIC_LIBS_${PROJECT_UP_NAME} "Build static libs" ON)
OPTION(ENABLE_QT_${PROJECT_UP_NAME} "Build Qt5 and Qt4 tools" ON)
OPTION(ENABLE_DOCU_${PROJECT_UP_NAME} "Build HTML documentation out of sources" ON)

IF(${CMAKE_SOURCE_DIR} STREQUAL ${CMAKE_CURRENT_SOURCE_DIR} AND NOT CMAKE_CROSSCOMPILING)
  OPTION(ENABLE_INSTALL_${PROJECT_UP_NAME} "Install files" ON)
ELSE()
  IF(CMAKE_CROSSCOMPILING)
    OPTION(ENABLE_INSTALL_${PROJECT_UP_NAME} "Install files" ON)
    IF(NOT ENABLE_TOOLS_${PROJECT_UP_NAME})
      SET(ENABLE_TOOLS_${PROJECT_UP_NAME} OFF)
    ENDIF(NOT ENABLE_TOOLS_${PROJECT_UP_NAME})
  ELSE()
    MESSAGE( "-- Oyjl: compile as subproject - skip install (ENABLE_INSTALL_${PROJECT_UP_NAME}=OFF)" )
    # disable install and build of shared libs for fallback subproject
    IF(NOT ENABLE_INSTALL_${PROJECT_UP_NAME})
      SET(ENABLE_INSTALL_${PROJECT_UP_NAME} OFF)
    ENDIF(NOT ENABLE_INSTALL_${PROJECT_UP_NAME})
  ENDIF()
  SET(ENABLE_STATIC_LIBS_${PROJECT_UP_NAME} ON)
  SET(ENABLE_SHARED_LIBS_${PROJECT_UP_NAME} OFF)
  SET(ENABLE_TESTS_${PROJECT_UP_NAME} OFF)
  SET(ENABLE_DOCU_${PROJECT_UP_NAME} OFF)
  MESSAGE( "-- ENABLE_STATIC_LIBS_${PROJECT_UP_NAME}: ${ENABLE_STATIC_LIBS_${PROJECT_UP_NAME}}" )
  MESSAGE( "-- ENABLE_SHARED_LIBS_${PROJECT_UP_NAME}: ${ENABLE_SHARED_LIBS_${PROJECT_UP_NAME}}" )
  MESSAGE( "-- ENABLE_TESTS_${PROJECT_UP_NAME}: ${ENABLE_TESTS_${PROJECT_UP_NAME}}" )
  MESSAGE( "-- ENABLE_TOOLS_${PROJECT_UP_NAME}: ${ENABLE_TOOLS_${PROJECT_UP_NAME}}" )
  MESSAGE( "-- ENABLE_DOCU_${PROJECT_UP_NAME}: ${ENABLE_DOCU_${PROJECT_UP_NAME}}" )
ENDIF()

# if our static code lands inside a shared lib, we need -fPIC
IF(ENABLE_SHARED_LIBS_${PROJECT_UP_NAME})
  SET( CMAKE_POSITION_INDEPENDENT_CODE ON)
ENDIF()

IF(CMAKE_CROSSCOMPILING)
  MESSAGE( "-- CMAKE_CROSSCOMPILING: " ${CMAKE_CROSSCOMPILING} )
ENDIF()

IF( CMAKE_BUILD_TYPE MATCHES "[Dd][Ee][Bb][Uu][Gg]" )
  MESSAGE( "-- Info build \"${CMAKE_BUILD_TYPE}\"" )
  SET( DEBUG 1 )
ENDIF()

#
# Target installation folders
#

SET (${PROJECT_UP_NAME}_INCLUDE_FOLDER
		${PROJECT_NAME}
		CACHE STRING
		"Optional folder below system include folder to install include files."
    )

IF(NOT CMAKE_MODULES_INSTALL_DIR)
  SET (CMAKE_MODULES_INSTALL_DIR
		"${CMAKE_INSTALL_LIBDIR}/cmake/${PROJECT_DOWN_NAME}"
		CACHE STRING
		"The folder where to install cmake files."
    )
ENDIF(NOT CMAKE_MODULES_INSTALL_DIR)


SET (TARGET_PKGCONFIG_FOLDER
		"pkgconfig"
		CACHE STRING
		"The folder below system library folder where to install pkgconfig files."
    )

IF( NOT DOC_PATH )
  SET( DOC_PATH "${CMAKE_SOURCE_DIR}/docs" )
ENDIF( NOT DOC_PATH )

# additional cmake modules for loading libraries
SET (CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${CMAKE_CURRENT_SOURCE_DIR}/cmake/modules/")

IF(USE_SYSTEM_YAJL)
  FIND_PACKAGE(Yajl REQUIRED)
ELSE()
  FIND_PACKAGE(Yajl)
ENDIF()
IF( HAVE_YAJL )
  INCLUDE_DIRECTORIES( ${YAJL_INCLUDE_DIRS} )
  LINK_DIRECTORIES( ${YAJL_LIBRARY_DIRS} )
  FIND_PATH(HAVE_YAJL_VERSION_H NAMES yajl/yajl_version.h HINTS ${PC_YAJL_INCLUDEDIR} ${PC_YAJL_INCLUDE_DIRS} PATH_SUFFIXES yajl)
  # yajl2 should come with yajl_version.h, the included yajl is version 1.0.x
  IF( HAVE_YAJL_VERSION_H )
    MESSAGE("-- yajl: ${PC_YAJL_VERSION} ${HAVE_YAJL_VERSION_H} ${YAJL_LIBRARY_DIRS}")
  ELSE( HAVE_YAJL_VERSION_H )
    INCLUDE_DIRECTORIES( ${CMAKE_CURRENT_SOURCE_DIR}/yajl )
    MESSAGE("-- yajl: trying internal headers")
  ENDIF( HAVE_YAJL_VERSION_H )
  SET( CONFIG_YAJL_LIBRARIES ${YAJL_LIBRARIES} )
ELSE( HAVE_YAJL )
  IF(CMAKE_VERSION VERSION_GREATER 2.8.0)
    #include(ExternalProject)
    #ExternalProject_Add( yajl
    #SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR}/yajl
    #CONFIGURE_COMMAND cmake ${CMAKE_CURRENT_SOURCE_DIR}/yajl
    #BINARY_DIR ${CMAKE_CURRENT_BINARY_DIR}/yajl
    #BUILD_COMMAND ${MAKE}
    # skip installation
    #INSTALL_COMMAND echo )
    INCLUDE_DIRECTORIES( ${CMAKE_CURRENT_SOURCE_DIR}/yajl )
    LINK_DIRECTORIES( ${CMAKE_CURRENT_BINARY_DIR}/yajl )
    # if our static code lands inside a shared lib, we need -fPIC
    SET( CMAKE_POSITION_INDEPENDENT_CODE ON)
    ADD_SUBDIRECTORY( yajl )
    SET( YAJL_LIBRARIES yajl-static )
    IF(NOT ${CMAKE_SOURCE_DIR} STREQUAL ${CMAKE_CURRENT_SOURCE_DIR})
      SET( YAJL_LIBRARIES ${YAJL_LIBRARIES} PARENT_SCOPE )
    ENDIF()
    SET( CONFIG_YAJL_LIBRARIES -L${CMAKE_INSTALL_PREFIX}/${CMAKE_INSTALL_LIBDIR} ${YAJL_LIBRARIES} )
  ENDIF()
ENDIF( HAVE_YAJL )

IF(USE_SYSTEM_LIBXML2)
  FIND_PACKAGE(LibXML2 REQUIRED)
ELSE()
  FIND_PACKAGE(LibXML2)
ENDIF()
IF(LIBXML2_FOUND)
  INCLUDE_DIRECTORIES( ${LIBXML2_INCLUDE_DIR} )
  LINK_DIRECTORIES( ${LIBXML2_LIBRARY_DIRS} )
  SET( HAVE_LIBXML2 1 )
  SET( CONFIG_LIBXML2_LIBRARIES "${LIBXML2_LIBRARIES} m" )
ELSE( LIBXML2_FOUND )
  IF(CMAKE_VERSION VERSION_GREATER 2.8.0)
    SET( HAVE_LIBXML2 TRUE  )
    INCLUDE_DIRECTORIES( ${CMAKE_CURRENT_SOURCE_DIR}/libxml2 )
    LINK_DIRECTORIES( ${CMAKE_CURRENT_BINARY_DIR}/libxml2 )
    ADD_SUBDIRECTORY( libxml2 )
    SET( LIBXML2_LIBRARIES xml2-static )
  ENDIF()
ENDIF(LIBXML2_FOUND)


INCLUDE(CheckLibraryExists)
INCLUDE(CheckIncludeFile)
CHECK_LIBRARY_EXISTS( yaml yaml_document_get_node "yaml.h" OYJL_HAVE_YAML )
IF( OYJL_HAVE_YAML )
    FIND_LIBRARY(YAML_LIBRARIES NAMES yaml libyaml yaml-static PATH)
    FIND_PATH(YAML_INCLUDE_DIR yaml.h)
    INCLUDE_DIRECTORIES( ${YAML_INCLUDE_DIR} )
    SET( CONFIG_YAML_LIBRARIES ${YAML_LIBRARIES} )
    MESSAGE("-- yaml: ${YAML_INCLUDE_DIR} : ${YAML_LIBRARIES}")
ENDIF( OYJL_HAVE_YAML )

IF( USE_SYSTEM_LIBXML2)
    FIND_PACKAGE(LibXML2 REQUIRED)
ELSE()
    FIND_PACKAGE(LibXML2)
ENDIF()
IF( LIBXML2_FOUND)
    INCLUDE_DIRECTORIES( ${LIBXML2_INCLUDE_DIR} )
    LINK_DIRECTORIES( ${LIBXML2_LIBRARY_DIRS} )
    MESSAGE("-- xml: ${LIBXML2_INCLUDE_DIR} : ${LIBXML2_LIBRARIES}")
    SET( OYJL_HAVE_LIBXML2 TRUE )
ENDIF( LIBXML2_FOUND )

CHECK_LIBRARY_EXISTS(m pow "math.h" HAVE_M)
IF(HAVE_M)
  FIND_LIBRARY( LIBM_LIBRARIES NAMES m )
  IF(LIBM_LIBRARIES)
    SET( EXTRA_CORE_LIBS ${EXTRA_LIBS_CORE} ${LIBM_LIBRARIES} )
  ENDIF(LIBM_LIBRARIES)
  MESSAGE( "-- m: ${LIBM_LIBRARIES}" )
ELSE(HAVE_M)
  MESSAGE( "-- m not found" )
ENDIF(HAVE_M)

CHECK_LIBRARY_EXISTS(dl dlopen "dlfcn.h" HAVE_DL)
IF(HAVE_DL)
  SET( EXTRA_CORE_LIBS ${EXTRA_CORE_LIBS} dl )
ELSE(HAVE_DL)
  FIND_PATH(DL_INCLUDE_DIR dlfcn.h)
  IF(DL_INCLUDE_DIR)
    SET( HAVE_DL 1 )
  ENDIF(DL_INCLUDE_DIR)
ENDIF(HAVE_DL)

CHECK_LIBRARY_EXISTS( c backtrace "execinfo.h" OYJL_HAVE_BACKTRACE )
CHECK_LIBRARY_EXISTS( microhttpd MHD_start_daemon "microhttpd.h" OYJL_HAVE_MHD )
IF(OYJL_HAVE_MHD)
  FIND_LIBRARY( MHD_LIBRARIES NAMES microhttpd )
  MESSAGE( "-- microhttpd: ${MHD_LIBRARIES}" )
ELSE(OYJL_HAVE_MHD)
  MESSAGE( "-- microhttpd: microhttpd not found" )
ENDIF(OYJL_HAVE_MHD)

FIND_PATH(LTDL_INCLUDE_DIR ltdl.h)
IF(LTDL_INCLUDE_DIR)
  SET( HAVE_LTDL_H 1 )
  CHECK_LIBRARY_EXISTS(ltdl dlopen "dlfcn.h" HAVE_LTDL)
  IF( HAVE_LTDL )
    SET( EXTRA_CORE_LIBS ${EXTRA_CORE_LIBS} ltdl )
  ENDIF( HAVE_LTDL )
ENDIF(LTDL_INCLUDE_DIR)

IF(NOT HAVE_DL AND NOT HAVE_LTDL)
  MESSAGE( SEND_ERROR "need one of libdl with dlfcn.h or libltdl with ltdl.h" )
ENDIF(NOT HAVE_DL AND NOT HAVE_LTDL)  

IF(ENABLE_QT_${PROJECT_UP_NAME})
  MESSAGE( "-- " ENABLE_QT_${PROJECT_UP_NAME} )
  FIND_PACKAGE(Qt5 COMPONENTS Widgets Qml Quick Svg LinguistTools)
ENDIF()
IF(ENABLE_STATIC_LIBS_${PROJECT_UP_NAME})
  MESSAGE( "-- ENABLE_STATIC_LIBS_${PROJECT_UP_NAME}" )
  IF(ENABLE_QT_${PROJECT_UP_NAME})
    IF(Qt5Qml_FOUND)
      MESSAGE( "-- Qt5Qml:                   " ${Qt5Qml_DIR} )
      SET( HAVE_QT TRUE )
      SET( QT_RENDER_LIB oyjl-args-qml-static )
    ENDIF()
  ENDIF()
ENDIF()
IF(ENABLE_SHARED_LIBS_${PROJECT_UP_NAME})
  MESSAGE( "-- ENABLE_SHARED_LIBS_${PROJECT_UP_NAME}" )
  IF(ENABLE_QT_${PROJECT_UP_NAME})
    IF(Qt5Qml_FOUND)
      UNSET( QT_RENDER_LIB )
    ENDIF()
  ENDIF()
ENDIF()

CHECK_INCLUDE_FILE(regex.h      OYJL_HAVE_REGEX_H)
  
CHECK_INCLUDE_FILE(langinfo.h   OYJL_HAVE_LANGINFO_H)
CHECK_INCLUDE_FILE(locale.h     OYJL_HAVE_LOCALE_H)
CHECK_INCLUDE_FILE(libintl.h    OYJL_HAVE_LIBINTL_H)
FIND_PACKAGE( GetText )
MESSAGE( "-- GetText: ${GETTEXT_FOUND} libintl: ${OYJL_HAVE_LIBINTL_H}" )
IF( GETTEXT_FOUND AND OYJL_HAVE_LIBINTL_H )
  SET( OYJL_USE_GETTEXT TRUE )
  SET( ${PROJECT_UP_NAME}_USE_GETTEXT TRUE )
  SET( ${PROJECT_UP_NAME}_LOCALEDIR "${CMAKE_INSTALL_FULL_LOCALEDIR}" )

  SET( ${PROJECT_UP_NAME}_LINGUAS ${CMAKE_SOURCE_DIR}/po/cs.po
               ${CMAKE_SOURCE_DIR}/po/de.po
               ${CMAKE_SOURCE_DIR}/po/eo.po
               ${CMAKE_SOURCE_DIR}/po/eu.po
               ${CMAKE_SOURCE_DIR}/po/fr.po
               ${CMAKE_SOURCE_DIR}/po/ru.po
     )
  FOREACH( lang ${${PROJECT_UP_NAME}_LINGUAS} )
    GET_FILENAME_COMPONENT(_absFile ${lang} ABSOLUTE)
    GET_FILENAME_COMPONENT(_abs_PATH ${_absFile} PATH)
    GET_FILENAME_COMPONENT(_lang ${_absFile} NAME_WE)
    SET( ${PROJECT_UP_NAME}_LINGUAS_SHORT ${${PROJECT_UP_NAME}_LINGUAS_SHORT} ${_lang} )
    SET( LINGUAS_GMO_FILES ${LINGUAS_GMO_FILES} " ${CMAKE_BINARY_DIR}/src/${_lang}.gmo" )
  ENDFOREACH( lang )
  SET( OYJL_LINGUAS_FULL cs_CZ de_DE eo eu fr_FR ru_RU )
ENDIF()
INCLUDE_DIRECTORIES( ${CMAKE_CURRENT_SOURCE_DIR}/po )

CONFIGURE_FILE(
    "${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_DOWN_NAME}_version.h.in"
    "${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_DOWN_NAME}_version.h"
    IMMEDIATE @ONLY)
INCLUDE_DIRECTORIES( ${CMAKE_CURRENT_BINARY_DIR} )
CONFIGURE_FILE(
    "${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_DOWN_NAME}_internal.h.in"
    "${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_DOWN_NAME}_internal.h"
    IMMEDIATE @ONLY)
INCLUDE_DIRECTORIES( ${CMAKE_CURRENT_BINARY_DIR} )


#cmake settings for use by find_package + OYJL_DOCUMENT_TOOL
CONFIGURE_FILE (
		"${CMAKE_CURRENT_SOURCE_DIR}/cmake/${PROJECT_NAME}Config.cmake.in"
		"${CMAKE_CURRENT_BINARY_DIR}/cmake/${PROJECT_NAME}Config.cmake"
		@ONLY
	)


## library 

SET( CFILES_OYJL_CORE_PUBLIC
	${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_DOWN_NAME}_core.c
	${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_DOWN_NAME}_debug.c
	${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_DOWN_NAME}_message.c
	${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_DOWN_NAME}_string.c
	${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_DOWN_NAME}_io.c
	${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_DOWN_NAME}_render.c
	${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_DOWN_NAME}_init.c
	${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_DOWN_NAME}_i18n.c
	${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_DOWN_NAME}_args.c
	${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_DOWN_NAME}_tree.c
	${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_DOWN_NAME}_args_tree.c
   )
 SET_SOURCE_FILES_PROPERTIES( "${CMAKE_CURRENT_SOURCE_DIR}/oyjl_args.c" PROPERTIES COMPILE_DEFINITIONS OYJL_INTERNAL=1 )
SET( CFILES_OYJL_ARGS_BASE_PUBLIC
	${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_DOWN_NAME}_args.c
   )
SET( CFILES_OYJL_YAJL_PUBLIC
	${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_DOWN_NAME}_yajl.c
   )
MACRO( CONVERT_TEXT_FILE_TO_HEADER ESCAPE )
  # Generate define without dot '.'
  STRING(REPLACE "." "_" ESCAPE_DEF ${ESCAPE})
  # Replace file name extension by regex
  STRING(REGEX REPLACE "\\.[^.]*$" ".h" ESCAPE_H ${ESCAPE})
  # Escape quote '"' and put a line break inside string and in code to keep text readable on output
  ADD_CUSTOM_COMMAND(
    OUTPUT  ${ESCAPE_H}
    COMMAND echo "/* Automatically generated from ${ESCAPE} . Will be overwritten on next build. */" > ${ESCAPE_H}
    COMMAND echo "#define ${ESCAPE_DEF} \"\\n\\" >> ${ESCAPE_H}
    COMMAND cat ${CMAKE_CURRENT_SOURCE_DIR}/${ESCAPE} | sed s/$/\\\\n\\\\/ | sed s/\\\"/\\\\\"/g >> ${ESCAPE_H}
    COMMAND echo "\"" >> ${ESCAPE_H}
    COMMENT "Convert ${ESCAPE} to C header ${ESCAPE_H}" VERBATIM )
ENDMACRO( CONVERT_TEXT_FILE_TO_HEADER )
CONVERT_TEXT_FILE_TO_HEADER( "${PROJECT_DOWN_NAME}_args_web_layout.css" )
CONVERT_TEXT_FILE_TO_HEADER( "${PROJECT_DOWN_NAME}_args_web_normalize.css" )
SET( CFILES_OYJL_ARGS_WEB
  ${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_DOWN_NAME}_args_web_layout.h
  ${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_DOWN_NAME}_args_web_normalize.h
	${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_DOWN_NAME}_args_web.c
	${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_DOWN_NAME}_io_share.c
   )
SET( CFILES_OYJL_EXTRA
	${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_DOWN_NAME}-args-share.c
   )
SET( CHEADERS_OYJL_PUBLIC
	${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_DOWN_NAME}_macros.h
	${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_DOWN_NAME}_macros_compile.h
	${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_DOWN_NAME}_args_base.h
	${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_DOWN_NAME}_args.h
	${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_DOWN_NAME}_debug.h
	${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_DOWN_NAME}_i18n.h
	${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_DOWN_NAME}_io.h
	${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_DOWN_NAME}_string.h
	${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_DOWN_NAME}_tree.h
	${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_DOWN_NAME}_test.h
	${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_DOWN_NAME}_test_main.h
  ${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_DOWN_NAME}_version.h
	${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_DOWN_NAME}.h
   )
 SET( CHEADERS_OYJL_PRIVATE
	${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_DOWN_NAME}_i18n_internal.h
	${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_DOWN_NAME}_tree_internal.h
   )
 IF( NOT OYJL_USE_GETTEXT )
  SET( CHEADERS_OYJL_PRIVATE ${CHEADERS_OYJL_PRIVATE}
    ${CMAKE_CURRENT_SOURCE_DIR}/po/lib${PROJECT_DOWN_NAME}.i18n.h
     )
 ENDIF( NOT OYJL_USE_GETTEXT )
SET( CHEADERS_OYJL
  ${CHEADERS_OYJL_PRIVATE}
	${CHEADERS_OYJL_PUBLIC}
   )

SET( SOURCES_CORE ${CFILES_OYJL_CORE_PUBLIC} ${CHEADERS_OYJL} )
SET( SOURCES_ARGS_BASE ${CFILES_OYJL_ARGS_BASE_PUBLIC} ${CHEADERS_OYJL} )
SET( SOURCES_PARSER ${CFILES_OYJL_YAJL_PUBLIC} ${CHEADERS_OYJL} )
SET( SOURCES_EXTRA ${CFILES_OYJL_EXTRA} ${CHEADERS_OYJL} )
SET( EXTRA_LIBS ${EXTRA_LIBS} ${YAJL_LIBRARIES} ${LIBXML2_LIBRARIES} ${YAML_LIBRARIES} ${EXTRA_CORE_LIBS} )
MESSAGE( "-- EXTRA_CORE_LIBS:          ${EXTRA_CORE_LIBS}" )
MESSAGE( "-- EXTRA_LIBS:               ${EXTRA_LIBS}" )

SET_SOURCE_FILES_PROPERTIES( "${CMAKE_CURRENT_SOURCE_DIR}/oyjl_core.c" PROPERTIES COMPILE_DEFINITIONS "OYJL_SKIP_TRANSLATE" )
SET( TOOL oyjl-bootstrap )
ADD_EXECUTABLE( ${TOOL} oyjl.c ${SOURCES_CORE} ${SOURCES_PARSER} )
TARGET_LINK_LIBRARIES ( ${TOOL} ${EXTRA_CORE_LIBS} ${EXTRA_LIBS} )

SET( TOOL oyjl-args-bootstrap )
ADD_EXECUTABLE( ${TOOL} oyjl-args.c oyjl-args-share.c ${SOURCES_CORE} ${SOURCES_PARSER} )
TARGET_LINK_LIBRARIES ( ${TOOL} ${EXTRA_CORE_LIBS} ${EXTRA_LIBS} )

SET( TOOL oyjl-translate-bootstrap )
ADD_EXECUTABLE( ${TOOL} oyjl_translate.c ${SOURCES_CORE} ${SOURCES_PARSER} )
TARGET_LINK_LIBRARIES ( ${TOOL} ${EXTRA_CORE_LIBS} ${EXTRA_LIBS} )
SET_SOURCE_FILES_PROPERTIES( "${CMAKE_CURRENT_SOURCE_DIR}/oyjl_core.c" PROPERTIES COMPILE_DEFINITIONS "" )


IF(ENABLE_SHARED_LIBS_${PROJECT_UP_NAME})
  ADD_LIBRARY( ${PROJECT_NAME}Core SHARED ${SOURCES_CORE} )
  ADD_LIBRARY( ${PROJECT_NAME}ArgsBase SHARED ${SOURCES_ARGS_BASE} )
  ADD_LIBRARY( ${PROJECT_NAME} SHARED ${SOURCES_PARSER} ${CHEADERS_OYJL_PUBLIC} )
  ADD_LIBRARY( ${PROJECT_NAME}ArgsCli SHARED ${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_DOWN_NAME}_args_cli.c )
  IF(OYJL_HAVE_MHD)
    ADD_LIBRARY( ${PROJECT_NAME}ArgsWeb SHARED ${CFILES_OYJL_ARGS_WEB} )
  ENDIF(OYJL_HAVE_MHD)
  TARGET_LINK_LIBRARIES ( ${PROJECT_NAME}Core ${EXTRA_CORE_LIBS} )
  TARGET_LINK_LIBRARIES ( ${PROJECT_NAME} ${PROJECT_NAME}Core ${EXTRA_LIBS} )
  TARGET_LINK_LIBRARIES ( ${PROJECT_NAME}ArgsCli ${PROJECT_NAME} ${PROJECT_NAME}Core ${EXTRA_LIBS} )
  IF(OYJL_HAVE_MHD)
    TARGET_LINK_LIBRARIES ( ${PROJECT_NAME}ArgsWeb ${PROJECT_NAME} ${PROJECT_NAME}Core ${EXTRA_LIBS} ${MHD_LIBRARIES} )
  ENDIF(OYJL_HAVE_MHD)

  SET_TARGET_PROPERTIES( ${PROJECT_NAME} PROPERTIES
  PRIVATE_HEADER ""
  PUBLIC_HEADER "${CHEADERS_OYJL_PUBLIC}"
  RESOURCE ""
  )

  # versions for libraries
  SET_TARGET_PROPERTIES( ${PROJECT_NAME}Core ${PROJECT_NAME}
    PROPERTIES VERSION   ${${PROJECT_UP_NAME}_VERSION_MAJOR}.${${PROJECT_UP_NAME}_VERSION_MINOR}.${${PROJECT_UP_NAME}_VERSION_MICRO}
               SOVERSION ${${PROJECT_UP_NAME}_VERSION_MAJOR}
                     )
  SET_TARGET_PROPERTIES( ${PROJECT_NAME}ArgsBase ${PROJECT_NAME}
    PROPERTIES VERSION   ${${PROJECT_UP_NAME}_VERSION_MAJOR}.${${PROJECT_UP_NAME}_VERSION_MINOR}.${${PROJECT_UP_NAME}_VERSION_MICRO}
               SOVERSION ${${PROJECT_UP_NAME}_VERSION_MAJOR}
                     )
  SET_TARGET_PROPERTIES( ${PROJECT_NAME}ArgsCli ${PROJECT_NAME}
    PROPERTIES VERSION   ${${PROJECT_UP_NAME}_VERSION_MAJOR}.${${PROJECT_UP_NAME}_VERSION_MINOR}.${${PROJECT_UP_NAME}_VERSION_MICRO}
               SOVERSION ${${PROJECT_UP_NAME}_VERSION_MAJOR}
                     )
  IF(OYJL_HAVE_MHD)
    SET_TARGET_PROPERTIES( ${PROJECT_NAME}ArgsWeb ${PROJECT_NAME}
    PROPERTIES VERSION   ${${PROJECT_UP_NAME}_VERSION_MAJOR}.${${PROJECT_UP_NAME}_VERSION_MINOR}.${${PROJECT_UP_NAME}_VERSION_MICRO}
               SOVERSION ${${PROJECT_UP_NAME}_VERSION_MAJOR}
                     )
  ENDIF(OYJL_HAVE_MHD)

  SET( OYJL_ARGS_BASE_DEFINITIONS OYJL_ARGS_BASE )
  IF(HAVE_DL)
    SET( OYJL_ARGS_BASE_DEFINITIONS ${OYJL_ARGS_BASE_DEFINITIONS};HAVE_DL )
  ENDIF()
  SET_TARGET_PROPERTIES( ${PROJECT_NAME}ArgsBase PROPERTIES COMPILE_DEFINITIONS "${OYJL_ARGS_BASE_DEFINITIONS}" )

  IF(ENABLE_INSTALL_${PROJECT_UP_NAME})
    INSTALL( TARGETS       ${PROJECT_NAME}Core
         RUNTIME       DESTINATION ${CMAKE_INSTALL_BINDIR}
         FRAMEWORK     DESTINATION ${FRAMEWORK_DIR}
         LIBRARY       DESTINATION ${CMAKE_INSTALL_LIBDIR}
         ARCHIVE       DESTINATION ${CMAKE_INSTALL_LIBDIR}
         PUBLIC_HEADER DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/${PROJECT_DOWN_NAME}
         RESOURCE      DESTINATION ${CMAKE_INSTALL_DATAROOTDIR}
         )
    INSTALL( TARGETS       ${PROJECT_NAME}
         RUNTIME       DESTINATION ${CMAKE_INSTALL_BINDIR}
         FRAMEWORK     DESTINATION ${FRAMEWORK_DIR}
         LIBRARY       DESTINATION ${CMAKE_INSTALL_LIBDIR}
         ARCHIVE       DESTINATION ${CMAKE_INSTALL_LIBDIR}
         PUBLIC_HEADER DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/${PROJECT_DOWN_NAME}
         RESOURCE      DESTINATION ${CMAKE_INSTALL_DATAROOTDIR}
         )
    INSTALL( TARGETS       ${PROJECT_NAME}ArgsBase
         RUNTIME       DESTINATION ${CMAKE_INSTALL_BINDIR}
         FRAMEWORK     DESTINATION ${FRAMEWORK_DIR}
         LIBRARY       DESTINATION ${CMAKE_INSTALL_LIBDIR}
         ARCHIVE       DESTINATION ${CMAKE_INSTALL_LIBDIR}
         PUBLIC_HEADER DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/${PROJECT_DOWN_NAME}
         RESOURCE      DESTINATION ${CMAKE_INSTALL_DATAROOTDIR}
         )
    INSTALL( TARGETS       ${PROJECT_NAME}ArgsCli
         RUNTIME       DESTINATION ${CMAKE_INSTALL_BINDIR}
         FRAMEWORK     DESTINATION ${FRAMEWORK_DIR}
         LIBRARY       DESTINATION ${CMAKE_INSTALL_LIBDIR}
         ARCHIVE       DESTINATION ${CMAKE_INSTALL_LIBDIR}
         PUBLIC_HEADER DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/${PROJECT_DOWN_NAME}
         RESOURCE      DESTINATION ${CMAKE_INSTALL_DATAROOTDIR}
         )
    IF(OYJL_HAVE_MHD)
      INSTALL( TARGETS       ${PROJECT_NAME}ArgsWeb
         RUNTIME       DESTINATION ${CMAKE_INSTALL_BINDIR}
         FRAMEWORK     DESTINATION ${FRAMEWORK_DIR}
         LIBRARY       DESTINATION ${CMAKE_INSTALL_LIBDIR}
         ARCHIVE       DESTINATION ${CMAKE_INSTALL_LIBDIR}
         PUBLIC_HEADER DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/${PROJECT_DOWN_NAME}
         RESOURCE      DESTINATION ${CMAKE_INSTALL_DATAROOTDIR}
         )
    ENDIF(OYJL_HAVE_MHD)
  ENDIF(ENABLE_INSTALL_${PROJECT_UP_NAME})
  SET( OYJL_CORE_LIB ${PROJECT_NAME}Core )
  SET( OYJL_TARGET_LIB ${PROJECT_NAME} )
ENDIF(ENABLE_SHARED_LIBS_${PROJECT_UP_NAME})

IF(ENABLE_STATIC_LIBS_${PROJECT_UP_NAME} AND NOT TARGET ${PACKAGE_NAME}-core-static)
  ADD_LIBRARY( ${PACKAGE_NAME}-core-static STATIC ${SOURCES_CORE} )
  SET( COMPILE_DEFINITIONS_STATIC "COMPILE_STATIC=1" )
  SET_TARGET_PROPERTIES( ${PACKAGE_NAME}-core-static PROPERTIES COMPILE_DEFINITIONS "${COMPILE_DEFINITIONS_STATIC}" )
  TARGET_LINK_LIBRARIES( ${PACKAGE_NAME}-core-static ${EXTRA_CORE_LIBS} )

  ADD_LIBRARY( ${PACKAGE_NAME}-static STATIC ${SOURCES_PARSER} ${CHEADERS_OYJL_PUBLIC} )
  TARGET_LINK_LIBRARIES( ${PACKAGE_NAME}-static ${PACKAGE_NAME}-core-static ${EXTRA_LIBS} )

  ADD_LIBRARY( ${PACKAGE_NAME}-args-cli-static STATIC  ${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_DOWN_NAME}_args_cli.c )
  TARGET_LINK_LIBRARIES( ${PACKAGE_NAME}-args-cli-static ${PACKAGE_NAME}-static ${PACKAGE_NAME}-core-static ${EXTRA_LIBS} )

  IF(OYJL_HAVE_MHD)
    IF(HAVE_QT)
      SET( QT_RENDER_LIB_FOR_WEB oyjl-args-qml-static )
      ADD_LIBRARY( ${PACKAGE_NAME}-args-web-static-client STATIC ${CFILES_OYJL_ARGS_WEB} )
      SET_TARGET_PROPERTIES(${PACKAGE_NAME}-args-web-static-client PROPERTIES COMPILE_DEFINITIONS "${COMPILE_DEFINITIONS_STATIC};NO_RENDER_START=1" )
      TARGET_LINK_LIBRARIES(${PACKAGE_NAME}-args-web-static-client
                            ${PACKAGE_NAME}-static
                            ${PACKAGE_NAME}-core-static
                            ${EXTRA_LIBS}
                            ${MHD_LIBRARIES} )
    ENDIF()
    ADD_LIBRARY( ${PACKAGE_NAME}-args-web-static STATIC ${CFILES_OYJL_ARGS_WEB} )
    SET_TARGET_PROPERTIES(  ${PACKAGE_NAME}-args-web-static PROPERTIES COMPILE_DEFINITIONS "${COMPILE_DEFINITIONS_STATIC}" )
    TARGET_LINK_LIBRARIES(  ${PACKAGE_NAME}-args-web-static
                            ${PACKAGE_NAME}-args-cli-static
                            ${PACKAGE_NAME}-static
                            ${PACKAGE_NAME}-core-static
                            ${EXTRA_LIBS}
                            ${MHD_LIBRARIES} )
  ENDIF(OYJL_HAVE_MHD)

  IF(ENABLE_INSTALL_${PROJECT_UP_NAME})
    INSTALL( TARGETS       ${PACKAGE_NAME}-core-static
           RUNTIME       DESTINATION ${CMAKE_INSTALL_BINDIR}
           FRAMEWORK     DESTINATION ${FRAMEWORK_DIR}
           LIBRARY       DESTINATION ${CMAKE_INSTALL_LIBDIR}
           ARCHIVE       DESTINATION ${CMAKE_INSTALL_LIBDIR}
           PUBLIC_HEADER DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/${PROJECT_DOWN_NAME}
           RESOURCE      DESTINATION ${CMAKE_INSTALL_DATAROOTDIR}/color/settings
         )
    INSTALL( TARGETS       ${PACKAGE_NAME}-static
           RUNTIME       DESTINATION ${CMAKE_INSTALL_BINDIR}
           FRAMEWORK     DESTINATION ${FRAMEWORK_DIR}
           LIBRARY       DESTINATION ${CMAKE_INSTALL_LIBDIR}
           ARCHIVE       DESTINATION ${CMAKE_INSTALL_LIBDIR}
           PUBLIC_HEADER DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/${PROJECT_DOWN_NAME}
           RESOURCE      DESTINATION ${CMAKE_INSTALL_DATAROOTDIR}/color/settings
         )
    INSTALL( TARGETS       ${PACKAGE_NAME}-args-cli-static
           RUNTIME       DESTINATION ${CMAKE_INSTALL_BINDIR}
           FRAMEWORK     DESTINATION ${FRAMEWORK_DIR}
           LIBRARY       DESTINATION ${CMAKE_INSTALL_LIBDIR}
           ARCHIVE       DESTINATION ${CMAKE_INSTALL_LIBDIR}
           PUBLIC_HEADER DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/${PROJECT_DOWN_NAME}
           RESOURCE      DESTINATION ${CMAKE_INSTALL_DATAROOTDIR}/color/settings
         )
    IF(OYJL_HAVE_MHD)
      INSTALL( TARGETS       ${PACKAGE_NAME}-args-web-static
           RUNTIME       DESTINATION ${CMAKE_INSTALL_BINDIR}
           FRAMEWORK     DESTINATION ${FRAMEWORK_DIR}
           LIBRARY       DESTINATION ${CMAKE_INSTALL_LIBDIR}
           ARCHIVE       DESTINATION ${CMAKE_INSTALL_LIBDIR}
           PUBLIC_HEADER DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/${PROJECT_DOWN_NAME}
           RESOURCE      DESTINATION ${CMAKE_INSTALL_DATAROOTDIR}/color/settings
         )
    ENDIF(OYJL_HAVE_MHD)
    IF(NOT ENABLE_SHARED_LIBS_${PROJECT_UP_NAME})
      INSTALL( FILES ${CHEADERS_OYJL_PUBLIC}
             DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/${PROJECT_DOWN_NAME} )
    ENDIF(NOT ENABLE_SHARED_LIBS_${PROJECT_UP_NAME})
  ENDIF(ENABLE_INSTALL_${PROJECT_UP_NAME})
  IF( NOT OYJL_TARGET_LIB )
    SET( OYJL_CORE_LIB ${PACKAGE_NAME}-core-static )
    SET( OYJL_TARGET_LIB ${PACKAGE_NAME}-static )
  ENDIF( NOT OYJL_TARGET_LIB )
  SET( OYJL_CORE_LIB_STATIC ${PACKAGE_NAME}-core-static )
  SET( OYJL_TARGET_LIB_STATIC ${PACKAGE_NAME}-static )
ENDIF(ENABLE_STATIC_LIBS_${PROJECT_UP_NAME} AND NOT TARGET ${PACKAGE_NAME}-core-static)

ADD_LIBRARY( ${PACKAGE_NAME}-extra-static STATIC ${SOURCES_EXTRA} ${CHEADERS_OYJL_PUBLIC} )
TARGET_LINK_LIBRARIES( ${PACKAGE_NAME}-extra-static ${PACKAGE_NAME}-core-static ${EXTRA_CORE_LIBS} )

IF(NOT ${CMAKE_SOURCE_DIR} STREQUAL ${CMAKE_CURRENT_SOURCE_DIR})
  SET( EXTRA_LIBS ${EXTRA_LIBS} PARENT_SCOPE )
ENDIF()

MACRO( BUILD_TOOL _NAME _SOURCES _DEPENDENCIES _LIBS _DOCUMENT _INSTALL _TARGET_LINK_LIBRARIES )
  SET( TOOL_ ${_NAME} )
  SET( TOOL_SOURCES_ ${_SOURCES} )
  ADD_EXECUTABLE( ${TOOL_} ${TOOL_SOURCES_} )
  STRING(LENGTH "${_DEPENDENCIES}" HAS_DEPENDENCIES)
  IF( HAS_DEPENDENCIES )
#    MESSAGE( "-- TOOL: " ${_NAME} " _DEPENDENCIES: " ${_DEPENDENCIES} )
    ADD_DEPENDENCIES( ${TOOL_} ${_DEPENDENCIES} )
  ELSE()
#    MESSAGE( "-- TOOL: " ${_NAME} )
  ENDIF( )
  IF(NOT HAVE_QT)
#    SET_TARGET_PROPERTIES( ${TOOL_} PROPERTIES COMPILE_DEFINITIONS "NO_OYJL_ARGS_RENDER=1" )
  ENDIF()
  IF(${_TARGET_LINK_LIBRARIES})
    TARGET_LINK_LIBRARIES( ${TOOL_} ${QT_RENDER_LIB} ${OYJL_TARGET_LIB} ${EXTRA_LIBS} ${_LIBS} )
  ENDIF()
  IF( ${_DOCUMENT} )
    OYJL_DOCUMENT_TOOL( ${TOOL_} ${TOOL_SOURCES_} "de_DE.UTF8" ${DOC_PATH} )
    OYJL_COMPLETION_TOOL( ${TOOL_} ${TOOL_SOURCES_} )
    SET( TOOLS ${TOOLS} ${TOOL_} )
  ENDIF( ${_DOCUMENT} )
  IF(ENABLE_INSTALL_${PROJECT_UP_NAME} AND ${_INSTALL})
    INSTALL (TARGETS ${TOOL_} DESTINATION ${CMAKE_INSTALL_BINDIR})
  ENDIF(ENABLE_INSTALL_${PROJECT_UP_NAME} AND ${_INSTALL})
  UNSET( HAS_DEPENDENCIES )
  UNSET( TOOL_ )
  UNSET( TOOL_SOURCES_ )
ENDMACRO( BUILD_TOOL )

IF( OYJL_USE_GETTEXT )
  SET( TGMO_${PROJECT_UP_NAME} ${PROJECT_DOWN_NAME}_gmo )
ENDIF()

INCLUDE( "${CMAKE_CURRENT_BINARY_DIR}/cmake/OyjlConfig.cmake" ) # OYJL_DOCUMENT_TOOL

IF(ENABLE_TOOLS_${PROJECT_UP_NAME})

  BUILD_TOOL( ${PACKAGE_NAME} "${CMAKE_CURRENT_SOURCE_DIR}/oyjl.c"                     ""                       ""                   TRUE  TRUE  TRUE )
  BUILD_TOOL( ${PACKAGE_NAME}-args "${CMAKE_CURRENT_SOURCE_DIR}/oyjl-args.c"           ""                       ""                   TRUE  TRUE  FALSE )
  TARGET_LINK_LIBRARIES( ${PACKAGE_NAME}-args ${QT_RENDER_LIB} ${PACKAGE_NAME}-extra-static ${OYJL_TARGET_LIB} ${EXTRA_LIBS} ${_LIBS} )
  BUILD_TOOL( ${PACKAGE_NAME}-translate "${CMAKE_CURRENT_SOURCE_DIR}/oyjl_translate.c" "${TGMO_${PROJECT_UP_NAME}}" ""               TRUE  TRUE  TRUE )
  IF(ENABLE_STATIC_LIBS_${PROJECT_UP_NAME})
    IF(HAVE_QT)
      SET( ${PROJECT_UP_NAME}_ARGS_QML_STATIC ${PACKAGE_NAME}-args-qml-static )
    ENDIF(HAVE_QT)
    BUILD_TOOL( ${PACKAGE_NAME}-translate-static "${CMAKE_CURRENT_SOURCE_DIR}/oyjl_translate.c" "${TGMO_${PROJECT_UP_NAME}}" ""      FALSE FALSE FALSE )
    TARGET_LINK_LIBRARIES( ${PACKAGE_NAME}-translate-static ${${PROJECT_UP_NAME}_ARGS_QML_STATIC} ${PACKAGE_NAME}-static ${EXTRA_LIBS} )
  ENDIF(ENABLE_STATIC_LIBS_${PROJECT_UP_NAME})
  BUILD_TOOL( jsontoyaml "${CMAKE_CURRENT_SOURCE_DIR}/oyjl.c"                          ""                       ""                   FALSE TRUE  TRUE )
  BUILD_TOOL( jsontoxml "${CMAKE_CURRENT_SOURCE_DIR}/oyjl.c"                           ""                       ""                   FALSE TRUE  TRUE )

  # needs libyaml
  IF( OYJL_HAVE_YAML )
    BUILD_TOOL( yamltojson "${CMAKE_CURRENT_SOURCE_DIR}/yaml2json.c"                   ""                       ${YAML_LIBRARIES}    FALSE TRUE TRUE )
  ENDIF( OYJL_HAVE_YAML )

  # needs libxml2
  IF(LIBXML2_FOUND)
    BUILD_TOOL( xmltojson "${CMAKE_CURRENT_SOURCE_DIR}/xml2json.c"                     ""                       ${LIBXML2_LIBRARIES} FALSE TRUE TRUE )
  ENDIF(LIBXML2_FOUND)
ENDIF(ENABLE_TOOLS_${PROJECT_UP_NAME})

IF(ENABLE_QT_${PROJECT_UP_NAME})
  ADD_SUBDIRECTORY( oyjl-args-qml )
ENDIF(ENABLE_QT_${PROJECT_UP_NAME})

SET( GETTEXT_TRANSLATIONS_TARGET_PREFIX ${PROJECT_DOWN_NAME}_ )
IF( ENABLE_TESTS_${PROJECT_UP_NAME} )
  FIND_PROGRAM(VALGRIND_EXECUTABLE valgrind)
  IF( VALGRIND_EXECUTABLE )
    SET( MEMCHECK ${VALGRIND_EXECUTABLE} --leak-check=full --show-leak-kinds=all )
  ENDIF( VALGRIND_EXECUTABLE )
  ADD_EXECUTABLE( oyjl-test-test "${CMAKE_CURRENT_SOURCE_DIR}/test-test.c" )
  TARGET_LINK_LIBRARIES( oyjl-test-test ${EXTRA_CORE_LIBS} )
  ADD_EXECUTABLE( oyjl-test-args "${CMAKE_CURRENT_SOURCE_DIR}/test-args.c" )
  TARGET_LINK_LIBRARIES( oyjl-test-args ${EXTRA_CORE_LIBS} )
  ADD_EXECUTABLE( oyjl-test-core "${CMAKE_CURRENT_SOURCE_DIR}/test-core.c" )
  TARGET_LINK_LIBRARIES( oyjl-test-core ${PACKAGE_NAME}-extra-static ${OYJL_CORE_LIB} ${EXTRA_CORE_LIBS} )
  ADD_EXECUTABLE( oyjl-test-core-static "${CMAKE_CURRENT_SOURCE_DIR}/test-core.c" )
  TARGET_LINK_LIBRARIES( oyjl-test-core-static ${PACKAGE_NAME}-extra-static ${PACKAGE_NAME}-core-static ${EXTRA_CORE_LIBS} )
  ADD_EXECUTABLE( oyjl-test "${CMAKE_CURRENT_SOURCE_DIR}/test.c" )
  TARGET_LINK_LIBRARIES( oyjl-test ${PACKAGE_NAME}-extra-static ${OYJL_TARGET_LIB} ${EXTRA_LIBS} )
  ADD_EXECUTABLE( oyjl-test-static "${CMAKE_CURRENT_SOURCE_DIR}/test.c" )
  TARGET_LINK_LIBRARIES( oyjl-test-static ${PACKAGE_NAME}-extra-static ${PACKAGE_NAME}-static ${PACKAGE_NAME}-core-static ${EXTRA_LIBS} )
  ADD_CUSTOM_TARGET(check-test
        OYJL_LOCALEDIR=${CMAKE_BINARY_DIR}/locale ${MEMCHECK} ./oyjl-test-test
        DEPENDS oyjl-test-test
        COMMENT "Test header only test framework" VERBATIM )
  ADD_CUSTOM_TARGET(check-args
        OYJL_LOCALEDIR=${CMAKE_BINARY_DIR}/locale ${MEMCHECK} ./oyjl-test-args
        DEPENDS oyjl-test-args check-test
        COMMENT "Test standalone Args API in oyjl-test-args" VERBATIM )
  ADD_CUSTOM_COMMAND(
        OUTPUT  oyjl-args-ui-lib.c
        COMMAND LANG=C ./oyjl-args -X export | ./oyjl-args -i - > oyjl-args-ui-lib.c
        DEPENDS oyjl-args
        COMMENT "Export UI and generate lib C code" VERBATIM
        )
  ADD_CUSTOM_COMMAND(
        OUTPUT  oyjl-args-ui.c
        COMMAND LANG=C ./oyjl-args -X export | ./oyjl-args -i - --c-stand-alone > oyjl-args-ui.c
        DEPENDS oyjl-args
        COMMENT "Export UI and generate stand alone C code" VERBATIM
        )
  INCLUDE_DIRECTORIES( ${CMAKE_CURRENT_SOURCE_DIR} )
  SET( TOOL oyjl-args-ui )
  SET( TOOLS ${TOOLS} ${TOOL} )
  SET( ${TOOL}_COMPILE_DEFINITIONS "OYJL_LOCALEDIR=\"${CMAKE_BINARY_DIR}/locale\"" )
  IF(OYJL_HAVE_LOCALE_H)
    SET( ${TOOL}_COMPILE_DEFINITIONS ${${TOOL}_COMPILE_DEFINITIONS};OYJL_HAVE_LOCALE_H )
  ENDIF()
  IF(OYJL_HAVE_LANGINFO_H)
    SET( ${TOOL}_COMPILE_DEFINITIONS ${${TOOL}_COMPILE_DEFINITIONS};OYJL_HAVE_LANGINFO_H )
  ENDIF()
  IF(OYJL_HAVE_LIBINTL_H)
    SET( ${TOOL}_COMPILE_DEFINITIONS ${${TOOL}_COMPILE_DEFINITIONS};OYJL_HAVE_LIBINTL_H )
  ENDIF()
  IF(OYJL_USE_GETTEXT)
    SET( ${TOOL}_COMPILE_DEFINITIONS ${${TOOL}_COMPILE_DEFINITIONS};OYJL_USE_GETTEXT )
    SET_SOURCE_FILES_PROPERTIES( "${TOOL}.c" PROPERTIES COMPILE_DEFINITIONS "OYJL_USE_GETTEXT=1" )
  ENDIF()
  MESSAGE( "${TOOL}_COMPILE_DEFINITIONS: " ${${TOOL}_COMPILE_DEFINITIONS} )
  SET_SOURCE_FILES_PROPERTIES( "${TOOL}.c" PROPERTIES COMPILE_DEFINITIONS "${${TOOL}_COMPILE_DEFINITIONS}" )
  ADD_EXECUTABLE( ${TOOL} oyjl-args-ui.c )
  ADD_CUSTOM_TARGET( check-${TOOL}
        ${MEMCHECK} ./${TOOL} -h
        DEPENDS ${TOOL} check-args
        COMMENT "Check Generated Stand Alone OyjlArgs Program" VERBATIM )
  UNSET( TOOL )
  SET( TOOL oyjl-args-ui-lib )
  SET( TOOLS ${TOOLS} ${TOOL} )
  SET( TOOL_SRC ${TOOL}.c )
  ADD_EXECUTABLE( ${TOOL} ${TOOL_SRC} )
  TARGET_LINK_LIBRARIES( ${TOOL} ${OYJL_TARGET_LIB} ${EXTRA_LIBS} )
  ADD_CUSTOM_TARGET( check-${TOOL}
        ${MEMCHECK} ./${TOOL} -h
        DEPENDS ${TOOL} check-oyjl-args-ui
        COMMENT "Check Generated Linked OyjlArgs Program" VERBATIM )
  UNSET( TOOL )
  SET( TOOL oyjl-args-ui-base )
  BUILD_TOOL( ${TOOL} "${CMAKE_CURRENT_SOURCE_DIR}/oyjl-args-ui-base.c"      ""                       ""                   FALSE FALSE FALSE )
  TARGET_LINK_LIBRARIES( ${TOOL} ${PACKAGE_NAME}-core-static )
  ADD_CUSTOM_TARGET( check-${TOOL}
        ${MEMCHECK} ./${TOOL} -h
        DEPENDS ${TOOL} check-oyjl-args-ui-lib
        COMMENT "Check Generated Static Linked OyjlArgsBase Program" VERBATIM )
  SET( TOOL oyjl-args-ui-base-lib )
  BUILD_TOOL( ${TOOL} "${CMAKE_CURRENT_SOURCE_DIR}/oyjl-args-ui-base.c"      ""                       ""                   FALSE FALSE FALSE )
  TARGET_LINK_LIBRARIES( ${TOOL} ${PROJECT_NAME}ArgsBase )
  ADD_CUSTOM_TARGET( check-${TOOL}
        ./${TOOL} -h
        DEPENDS ${TOOL} check-oyjl-args-ui-base
        COMMENT "Check Generated Linked OyjlArgsBase Program" VERBATIM )
  SET( TOOL oyjl-args-ui-base-oyjl-h )
  BUILD_TOOL( ${TOOL} "${CMAKE_CURRENT_SOURCE_DIR}/oyjl-args-ui-base.c"      ""                       ""                   FALSE FALSE FALSE )
  SET_TARGET_PROPERTIES( ${TOOL} PROPERTIES COMPILE_DEFINITIONS "USE_OYJL_H" )
  TARGET_LINK_LIBRARIES( ${TOOL} ${PROJECT_NAME}ArgsBase )
  ADD_CUSTOM_TARGET( check-${TOOL}
        ${MEMCHECK} ./${TOOL} -h
        DEPENDS ${TOOL} check-oyjl-args-ui-base-lib
        COMMENT "Check Generated Linked OyjlArgs Program" VERBATIM )
  ADD_CUSTOM_TARGET(check-core
        COMMAND OYJL_LOCALEDIR=${CMAKE_BINARY_DIR}/locale ${MEMCHECK} ./oyjl-test-core-static
        COMMAND LOCPATH=${CMAKE_BINARY_DIR}/locale ./oyjl-test-core-static 1
        DEPENDS ${GETTEXT_TRANSLATIONS_TARGET_PREFIX}translations oyjl-test-core-static check-oyjl-args-ui-base-oyjl-h oyjl
        COMMENT "Test libOyjlCore" VERBATIM )
  ADD_CUSTOM_TARGET(check
        LOCPATH=${CMAKE_BINARY_DIR}/locale ${MEMCHECK} ./oyjl-test-static
        DEPENDS oyjl-test-static check-core
        COMMENT "Test libOyjl" VERBATIM )
  FILE( GLOB ${PROJECT_UP_NAME}_TEST_EXTRA_SRC_FILES RELATIVE ${CMAKE_CURRENT_SOURCE_DIR} test-extra*.c )
  MESSAGE( ${PROJECT_UP_NAME}_TEST_EXTRA_SRC_FILES: "${${PROJECT_UP_NAME}_TEST_EXTRA_SRC_FILES}" )
  IF( ${${PROJECT_UP_NAME}_TEST_EXTRA_SRC_FILES} MATCHES "test-extra" )
    ADD_EXECUTABLE( oyjl-test-extra-static ${${PROJECT_UP_NAME}_TEST_EXTRA_SRC_FILES} )
    TARGET_LINK_LIBRARIES( oyjl-test-extra-static ${PACKAGE_NAME}-extra-static ${PACKAGE_NAME}-static ${PACKAGE_NAME}-static ${PACKAGE_NAME}-core-static ${EXTRA_LIBS} )
    ADD_CUSTOM_TARGET(check-extra
        OYJL_LOCALEDIR=${CMAKE_BINARY_DIR}/locale ${MEMCHECK} ./oyjl-test-extra-static
        DEPENDS oyjl-test-extra-static check
        COMMENT "Test Extra" VERBATIM )
  ENDIF()
  ADD_CUSTOM_TARGET( check-fast
        COMMAND OYJL_LOCALEDIR=${CMAKE_BINARY_DIR}/locale ./oyjl-test-test
        COMMAND OYJL_LOCALEDIR=${CMAKE_BINARY_DIR}/locale ./oyjl-test-args
        COMMAND ./oyjl-args-ui -h
        COMMAND OYJL_LOCALEDIR=${CMAKE_BINARY_DIR}/locale ./oyjl-test-core-static
        COMMAND LOCPATH=${CMAKE_BINARY_DIR}/locale ./oyjl-test-static
        COMMAND OYJL_LOCALEDIR=${CMAKE_BINARY_DIR}/locale ./oyjl-test-extra-static
        DEPENDS oyjl-test-test
        DEPENDS oyjl-test-args
        DEPENDS oyjl-args-ui
        DEPENDS oyjl-test-core-static
        DEPENDS oyjl-test-static
        DEPENDS oyjl-test-extra-static
        COMMENT "Check Fast" VERBATIM )
  # coverage testing
  IF(USE_GCOV AND CMAKE_COMPILER_IS_GNUCC)
    MESSAGE("testing coverage using gcc's gcov; try target: coverage")
    INCLUDE(CodeCoverage)
    SET(ENABLE_SHARED_LIBS_${PROJECT_UP_NAME} OFF)
    MESSAGE( "-- ENABLE_SHARED_LIBS_${PROJECT_UP_NAME}: ${ENABLE_SHARED_LIBS_${PROJECT_UP_NAME}} (USE_GCOV + GNUCC)" )
    SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -g -O0 -fprofile-arcs -ftest-coverage")
    SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -g -O0 -fprofile-arcs -ftest-coverage")
    SET(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} -g -O0 -fprofile-arcs -ftest-coverage")
    SET(CMAKE_MODULE_LINKER_FLAGS "${CMAKE_MODULE_LINKER_FLAGS} -g -O0 -fprofile-arcs -ftest-coverage")
    SETUP_TARGET_FOR_COVERAGE(
				coverage  # Name for custom target.
				oyjl-test-static # Name of the test driver executable that runs the tests.
									# NOTE! This should always have a ZERO as exit code
									# otherwise the coverage generation will not complete.
				coverage            # Name of output directory.
			)
    ADD_DEPENDENCIES( coverage oyjl oyjl-translate )
  ENDIF(USE_GCOV AND CMAKE_COMPILER_IS_GNUCC)
ENDIF( ENABLE_TESTS_${PROJECT_UP_NAME} )

IF(ENABLE_INSTALL_${PROJECT_UP_NAME})
  CONFIGURE_FILE(
    "${CMAKE_CURRENT_SOURCE_DIR}/cmake/${PROJECT_NAME}Uninstall.cmake.in"
    "${CMAKE_CURRENT_BINARY_DIR}/cmake/${PROJECT_NAME}Uninstall.cmake"
    IMMEDIATE @ONLY)

  ADD_CUSTOM_TARGET( uninstall
	  "${CMAKE_COMMAND}" -P "${CMAKE_CURRENT_BINARY_DIR}/cmake/${PROJECT_NAME}Uninstall.cmake")
ENDIF(ENABLE_INSTALL_${PROJECT_UP_NAME})

# i18n
IF( GETTEXT_FOUND AND OYJL_USE_GETTEXT )
  SET(_potFile ${CMAKE_CURRENT_SOURCE_DIR}/po/${PROJECT_DOWN_NAME}.pot)
  #SET( _xgettext_option_list --language=C --keyword=_ --keyword=N_ --keyword=C_:1c,2 --keyword=NC_:1c,2 -s --package-name=${PROJECT_DOWN_NAME} --package-version=${${PROJECT_NAME}_MAJOR_VERSION}.${${PROJECT_NAME}_MINOR_VERSION} )
  SET( _xgettext_option_list --add-comments --keyword=gettext --flag=gettext:1:pass-c-format --keyword=_ --flag=_:1:pass-c-format --keyword=N_ --flag=N_:1:pass-c-format  --copyright-holder='Kai-Uwe Behrmann'  --msgid-bugs-address='ku.b@gmx.de' --from-code=utf-8 --package-name=${PROJECT_DOWN_NAME} --package-version=${${PROJECT_NAME}_MAJOR_VERSION}.${${PROJECT_NAME}_MINOR_VERSION} )
  SET( XGETTEXT_CMD xgettext )
  FILE( GLOB ${PROJECT_UP_NAME}_SRC_FILES_TEST RELATIVE ${CMAKE_CURRENT_SOURCE_DIR}
        test*.c
      )
  FILE( GLOB ${PROJECT_UP_NAME}_SRC_FILES_CORE RELATIVE ${CMAKE_CURRENT_SOURCE_DIR}
        oyjl_*.c
      )
  SET( ${PROJECT_UP_NAME}_SRC_FILES
       oyjl-args.c
       oyjl.c
       ${${PROJECT_UP_NAME}_SRC_FILES_CORE}
       ${${PROJECT_UP_NAME}_SRC_FILES_TEST}
       xml2json.c
       yaml2json.c
       oyjl-args-qml/mini-app.c
       oyjl-args-qml/main.cpp
     )

  ADD_CUSTOM_COMMAND(OUTPUT ${_potFile}
    COMMAND ${XGETTEXT_CMD} ${_xgettext_option_list} -o ${_potFile} ${${PROJECT_UP_NAME}_SRC_FILES}
    DEPENDS ${${PROJECT_UP_NAME}_SRC_FILES}
    WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
    COMMENT "Extract translatable messages to ${_potFile}"
  )

  SET( ${PROJECT_UP_NAME}_LINGUAS po/cs.po po/de.po po/eo.po po/eu.po po/fr.po po/ru.po )
  # compile translations always to have them ready for installation
  GETTEXT_CREATE_TRANSLATIONS( ${_potFile} ALL ${${PROJECT_UP_NAME}_LINGUAS} )

  ADD_CUSTOM_COMMAND( OUTPUT ${PROJECT_DOWN_NAME}_gmo-cp
    COMMAND for lang in ${${PROJECT_UP_NAME}_LINGUAS_SHORT}\; do mkdir -p "${CMAKE_BINARY_DIR}/locale/$$lang/LC_MESSAGES/" \; cp "${CMAKE_CURRENT_BINARY_DIR}/$$lang.gmo" "${CMAKE_BINARY_DIR}/locale/$$lang/LC_MESSAGES/${PROJECT_DOWN_NAME}.mo" \; done
    COMMENT "put ${PROJECT_DOWN_NAME}.mo files into ${CMAKE_BINARY_DIR}/locale/lang/LC_MESSAGES/ path for testing with LOCPATH"
                    )
  ADD_CUSTOM_TARGET( ${PROJECT_DOWN_NAME}_gmo ALL
                     DEPENDS ${GETTEXT_TRANSLATIONS_TARGET_PREFIX}translations
                             ${PROJECT_DOWN_NAME}_gmo-cp
                              lib${PROJECT_DOWN_NAME}_i18n
                   )
  SET( LOCALES cs_CZ,de_DE,eo,eu_ES,fr_FR,ru_RU )
  FOREACH( TOOL ${TOOLS} )
    ADD_CUSTOM_COMMAND( OUTPUT ${TOOL}.i18n.json
      COMMAND export LANG=C && ./${TOOL} -X json > ${TOOL}.json
      COMMAND oyjl-translate-bootstrap -a -i ${TOOL}.json -o ${CMAKE_CURRENT_SOURCE_DIR}/${TOOL}.i18n.json -k name,description,help -d "oyjl" -p "${CMAKE_BINARY_DIR}/locale/" -l ${LOCALES} -t
      COMMAND oyjl-translate-bootstrap -a -i ${TOOL}.json -o ${CMAKE_CURRENT_SOURCE_DIR}/${TOOL}.i18n.c -k name,description,help,label -d "oyjl" -p "${CMAKE_BINARY_DIR}/locale/" -l ${LOCALES} -t -w=C
      DEPENDS oyjl-translate-bootstrap ${TOOL}
      COMMENT "generate ${TOOL}.i18n.json" )
    ADD_CUSTOM_TARGET( ${TOOL}_i18n ALL
      DEPENDS oyjl-bootstrap oyjl-translate-bootstrap ${GETTEXT_TRANSLATIONS_TARGET_PREFIX}translations ${TOOL}.i18n.json  )
  ENDFOREACH()
  SET( COLLECT_I18N "" )
  SET( COLLECT_I18N_DEPENDS "" )
  FOREACH( lang ${${PROJECT_UP_NAME}_LINGUAS} )
    GET_FILENAME_COMPONENT(_absFile ${lang} ABSOLUTE)
    GET_FILENAME_COMPONENT(_abs_PATH ${_absFile} PATH)
    GET_FILENAME_COMPONENT(_lang ${_absFile} NAME_WE)
    SET( COLLECT_I18N ${COLLECT_I18N} -i ${_lang}.i18n.json )
    SET( COLLECT_I18N_DEPENDS ${COLLECT_I18N_DEPENDS} ${_lang}.i18n.json )
    ADD_CUSTOM_COMMAND( OUTPUT ${_lang}.i18n.json
      COMMAND oyjl-translate-bootstrap -c -i ${CMAKE_CURRENT_SOURCE_DIR}/po/${_lang}.po -o ${_lang}.i18n.json --locale=${_lang}
      DEPENDS oyjl-translate-bootstrap ${CMAKE_CURRENT_SOURCE_DIR}/po/${_lang}.po
      COMMENT "generate ${_lang}.i18n.json" )
  ENDFOREACH( lang )
  ADD_CUSTOM_COMMAND( OUTPUT ${CMAKE_CURRENT_SOURCE_DIR}/po/lib${PROJECT_DOWN_NAME}.i18n.h
      COMMAND oyjl-bootstrap json ${COLLECT_I18N} -w oiJS -W lib${PROJECT_DOWN_NAME}_i18n > ${CMAKE_CURRENT_SOURCE_DIR}/po/lib${PROJECT_DOWN_NAME}.i18n.h
      COMMAND oyjl-bootstrap json ${COLLECT_I18N} > ${CMAKE_CURRENT_SOURCE_DIR}/po/lib${PROJECT_DOWN_NAME}.i18n.json
      DEPENDS ${GETTEXT_TRANSLATIONS_TARGET_PREFIX}translations ${COLLECT_I18N_DEPENDS}
      COMMENT "generate lib${PROJECT_DOWN_NAME}.i18n.h" )
  ADD_CUSTOM_TARGET( lib${PROJECT_DOWN_NAME}_i18n ALL
      DEPENDS oyjl-bootstrap ${CMAKE_CURRENT_SOURCE_DIR}/po/lib${PROJECT_DOWN_NAME}.i18n.h )
ENDIF()


#
# Misc.
#

IF(ENABLE_INSTALL_${PROJECT_UP_NAME})
  INSTALL ( FILES
		"${CMAKE_CURRENT_BINARY_DIR}/cmake/${PROJECT_NAME}Config.cmake"
		DESTINATION ${CMAKE_MODULES_INSTALL_DIR} COMPONENT dev
	)
  # KDE has problems rendering SVG blur
  INSTALL( FILES oyjl-args-qml/images/logo.png DESTINATION ${CMAKE_INSTALL_DATAROOTDIR}/pixmaps/ RENAME oyjl.png )
ENDIF(ENABLE_INSTALL_${PROJECT_UP_NAME})

#pkg-config files
CONFIGURE_FILE(
		"${CMAKE_CURRENT_SOURCE_DIR}/${PACKAGE_NAME}.pc.in"
		"${CMAKE_CURRENT_BINARY_DIR}/${PACKAGE_NAME}.pc"
		@ONLY
       )

IF(ENABLE_INSTALL_${PROJECT_UP_NAME})
  INSTALL( FILES
		"${CMAKE_CURRENT_BINARY_DIR}/${PACKAGE_NAME}.pc"
		DESTINATION ${CMAKE_INSTALL_LIBDIR}/${TARGET_PKGCONFIG_FOLDER}
       )
ENDIF(ENABLE_INSTALL_${PROJECT_UP_NAME})

IF(ENABLE_DOCU_${PROJECT_UP_NAME})
  ADD_SUBDIRECTORY( docs )
ENDIF(ENABLE_DOCU_${PROJECT_UP_NAME})

INCLUDE(FeatureSummary)
FEATURE_SUMMARY(WHAT ALL INCLUDE_QUIET_PACKAGES FATAL_ON_MISSING_REQUIRED_PACKAGES)
